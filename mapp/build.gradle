plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'com.google.gms.google-services'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('signing.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'com.fadlurahmanf.mapp'
    compileSdk 34

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId "com.fadlurahmanf.mapp"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "BASE_QURAN_URL", "\"TES\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs{
        release {
            if (keystoreProperties['release.keystore'] != null) {
                storeFile file(keystoreProperties['release.keystore'])
            }
            keyAlias keystoreProperties['release.keystore.alias']
            keyPassword keystoreProperties['release.keystore.keyPassword']
            storePassword keystoreProperties['release.keystore.storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    flavorDimensions "app"
    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            dimension "app"
        }
        staging {
            applicationIdSuffix ".staging"
            dimension "app"
        }
        production {
            dimension "app"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_19
        targetCompatibility JavaVersion.VERSION_19
    }
    kotlinOptions {
        jvmTarget = '19'
    }
}

kotlin {
    jvmToolchain(19)
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation 'com.google.dagger:dagger:2.47'
    kapt 'com.google.dagger:dagger-compiler:2.47'

    implementation(platform("com.google.firebase:firebase-bom:32.2.3"))
    implementation("com.google.firebase:firebase-analytics-ktx")

    implementation(project(":core_logger"))
    implementation(project(":core_network"))
    implementation(project(":core_platform"))

    implementation(project(":mapp_analytic"))
    implementation(project(":mapp_api"))
    implementation(project(":mapp_config"))
    implementation(project(":mapp_example"))
    implementation(project(":mapp_fcm"))
    implementation(project(":mapp_firebase_database"))
    implementation(project(":mapp_logger"))
    implementation(project(":mapp_notification"))
    implementation(project(":mapp_shared"))
    implementation(project(":mapp_splash"))
    implementation(project(":mapp_ui"))
}